! THIS SUBROUTINE CALCULATES THE SOLID TEMPERATURE PROFILE GIVEN
! THE PREVIOUS TIME STEP SOLID AND COOLANT TEMPERATURE PROFILES. AND
! THE LIQUID THERMAL CONDUCTIVITIES
!
SUBROUTINE TSOLID(TSOLD, TLOLD, HCL, TSNEW)
IMPLICIT NONE
!
! VARIABLES USED IN TSOLID
!
! TSOLD = VECTOR OF POROUS SOLID TEMPERATURE AT PREVIOUS TIME STEP
! KL = LOCAL COOLANT THERMAL CONDUCTIVITY
! DT=TIMESTEP
! DX = X DISTANCE STEP SIZE THROUGH WALL
! HCL(N) = PACKED BED OF SPHERES HEAT TRANSFER COEFFICIENT
! CO = PROPORTIONALITY CONSTANT USED TO DETERMINE SOLID SURFACE AREA
! R = POROUS SPHERE RADIUS
! EPS = POROSITY OF THE POROUS SOLID
! TLOLD = VECTOR OF TRANSPIRATION COOLANT TEMPERATURE AT PREVIOUS
! TIME STEP
! TB = BULK TEMPERATURE OF THE COOLANT IN THE COOLANT CHANNEL
! MDOTC = MASS FLOW RATE OF COOLANT IN COOLANT CHANNEL
! DH = HYDRAULIC DIAMETER OF COOLANT CHANNEL
! PB = COOLANT LINE PRESSURE (BULK CONDITIONS)
! PI=3.14159
! THROATD = NOZZLE AERODYNAMIC THROAT DIAMETER
! MDOTF = MASS FLOW RATE OF COMBUSTION PRODUCTS
! W = VISCOSITY, TEMPERATURE EXPONENT
! PH = HYDRAULIC PERIMETER OF COOLANT CHANNEL
! PBB = COOLANT CHANNEL PRESSURE FOR REGENERATIVE COOLING CASE
! GAM = RATIO OF SPECIFIC HEATS OF COMBUSTION PRODUCTS
! TOG = STAGNATION TEMPERATURE OF COMBUSTION PRODUCTS
! MDOTL = MASS FLOW RATE OF COOLANT THROUGH POROUS WALL
! TG = STATIC TEMPERATURE OF COMBUSTION PRODUCTS AT THROAT
! TSNEW = SOLID TEMPERATURE AT NEW TIME STEP
! RHOS = SOLID DENSITY
! CPS = SOLID SPECIFIC HEAT
! KW = SOLID THERMAL CONDUCTIVITY
! KS = EFFECTIVE THERMAL CONDUCTIVITY OF THE POROUS SOLID
! HCS = COLD GAS SIDE HEAT TRANSFER COEFFICIENT
! TWH = HOT GAS SIDE SOLID WALL TEMPERATURE
! TAW = ADIABATIC WALL TEMPERATURE
! HG = HOT GAS SIDE HEAT TRANSFER COEFFICIENT
! STAB 1 = STABILITY CONSTRAINT
! STAB2 = STABILITY CONSTRAINT
! STAB3 = STABILITY CONSTRATNT
! MUOF = COMBUSTION PRODUCTS VISCOSITY AT TOG
! CPOF = COMBUSTION PRODUCTS SPECIFIC HEAT AT TOG
! PROF = COMBUSTION PRODUCTS PRANDTL NUMBER AT TOG
! N = INDEX USED FOR SOLID TEMPERATURE ARRAY
! M = INDEX USED FOR COOLANT TEMPERATURE ARRAY
! LASTX = GRID SIZE
!
! DECLARE VARIABLES
!
DOUBLE PRECISION TSOLD, KL, DT, DX, HCL, C0, R, EPS, TLOLD, TB
DOUBLE PRECISION MDOTC, DH, PB, PI, THROATD, MDOTF, W, PH, PBB
DOUBLE PRECISION GAM, T0G, MDOTL, TG, TSNEW, RHOS, CPS, KW
DOUBLE PRECISION KS, HCS, TWH, TAW, HG, STAB1, STAB2, STAB3
DOUBLE PRECISION MU0F, CP0F, PR0F
INTEGER N, M, LASTX
CHARACTER*64 FILE3
!
! DIMENSION VARIABLES THAT ARE ARRAYS
!
DIMENSION TSOLD(20000), KL(20000), TLOLD(20000),HCL(20000)
DIMENSION TSNEW(20000)

COMMON /ERROR_STAB_FILEBLK/FILE3
!
! THIS COMMON BLOCK CONTAINS THE GRID DIMENSIONS USED
!
COMMON /DIMEN/LASTX, DX
!
! THIS COMMON BLOCK CONTAINS OUTPUT VARIABLES FROM TSOLID
!
COMMON /SOLBLK/HCS, HG, TAW
!
! THIS COMMON BLOCK CONTAINS VARIABLES COMMON TO TSOLID AND TLIQUID
!
COMMON /TSTL/C0, EPS, R, PB, TB, KW, MDOTL
!
! THIS COMMON BLOCK CONTAINS VARIABLES COMMON TO TSOLID ONLY
!
COMMON /TSBLK/RHOS, CPS, MDOTC, DH, PI, THROATD, MDOTF
COMMON /TSBLK/PH, W, GAM, T0G, TG, PBB, MU0F, CP0F, PR0F
!
! BEGIN THE MAIN LOOP TO MARCH THROUGH THE POROUS WALL
!
DO N= 1,(LASTX+1)
!
! SYNCHRONIZE THE LIQUID AND SOLID INDICES
!
M=N+2
!
! CALCULATE THE EFFECTIVE THERMAL CONDUCTIVITY OF THE SOLID
!
!KS = KL(N)*( (2.0*KL(N)+KW)-2.0*(1.0-EPS)*(KL(N)-KW)/(2.0*KL(N)+KW+(1.0-EPS)*(KL(N)-KW)) )
KS=(1.0-EPS)*KW
!
! INITIALIZE THE STABILITY CONSTRAINT CONSTANTS
!
STAB1 = 1.0
STAB2 = 1.0
STAB3 = 1.0
!
! IF CALCULATING THE COLD GAS SIDE BOUNDARY TEMPERATURE USE THIS
! IF-THEN LOOP
!
IF (N.EQ.1) THEN
!
! GENERATE THE COLD GAS SIDE HEAT TRANSFER COEFFICIENT
!
CALL HCOOLSIDE(PB, TB, TSOLD(N), MDOTC, DH, PH, HCS)
!
! GENERATE THE STABLE TIME STEP FOR THIS LOCATION
!Test this subrontine alone HCL(N)=0 HCS=10^20
!HCL(N)=0
!HCS=1.0D4

DT = 0.9/( (2.0*KS)/((DX**2.0)*RHOS*CPS)+(HCL(N)*C0)/(RHOS*R*CPS)+(2.0*HCS)/(RHOS*DX*CPS)) 
!
! CALCULATE THE NEW TEMPERATURE AT THE COLD GAS SIDE BOUNDARY
!
TSNEW(N) =((2.0*KS*DT)/((DX**2.0)*RHOS*CPS))*(TSOLD(N+1)-TSOLD(N))-((HCL(N)*C0*DT)/(RHOS*R*CPS))*(TSOLD(N)-TLOLD(M))-((2.0*HCS*DT)/(RHOS*DX*CPS))*(TSOLD(N)-TB)+TSOLD(N)
!
! CALLCULATE STABILITY CONSTRAINT
!
STAB1 = 1.0/((2.0*KS)/((DX**2.0)*RHOS*CPS)+(HCL(N)*C0)/(RHOS*R*CPS)+(2.0*HCS)/(RHOS*DX*CPS))
END IF
!
! IF CALCULATING AN INTERIOR NODE POINT USE THIS IF-THEN LOOP
!
IF ((N.NE.1).AND.(N.NE.(LASTX+1))) THEN
!
! CALCULATE THE TIME STEP FOR THIS LOCATION
!Test this subrontine alone HCL(N)=0
!HCL(N)=0
DT = 0.9/((2.0*KS)/((DX**2.0)*RHOS*CPS)+(HCL(N)*C0)/(RHOS*R*CPS))
!
!CALCULATE THE NEW TEMPERATURE AT THIS LOCATION
!
TSNEW(N) = ((KS*DT)/((DX**2.0)*RHOS*CPS))*(TSOLD(N+1)+TSOLD(N-1)-2.0*TSOLD(N))-((HCL(N)*C0*DT)/(RHOS*R*CPS))*(TSOLD(N)-TLOLD(M))+TSOLD(N)                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                          
!
! CALCULATE STABILITY CONSTRAINT
!
STAB2 = 1.0/((2.0*KS)/((DX**2.0)*RHOS*CPS)+(HCL(N)*C0)/(RHOS*R*CPS))
END IF
!
! IF CALCULATING THE HOT GAS SIDE BOUNDARY TEMPERATURE THEN USE
! THIS IF-THEN LOOP
!
IF (N.EQ.(LASTX+1)) THEN
!
! ASSIGN THE PREVIOUS TIME STEP TEMPERATURE TO THE HOT GAS SIDE
! WALL TEMPERATURE
!
TWH = TSOLD(N)
!

!GENERATE THE ADIABATIC WALL TEMPERATURE
CALL FINDTAW(TG, T0G, TWH, TAW)
! GENERATE THE HOT GAS SIDE HEAT TRANSFER COEFFICIENT
CALL HOTGAS(TWH, THROATD, MDOTF, W, GAM, T0G, TG, &
 MDOTL, PI, EPS, TAW, MU0F, CP0F, PR0F, HG)
!
! CALCULATE A TIME STEP FOR THIS LOCATION
!Test this subrontine alone HCL(N)=0 HG=10^20
!HCL(N)=0
!HG=2.0D4

DT = 0.9/((2.0*KS)/((DX**2.0)*RHOS*CPS)+(HCL(N)*C0)/(RHOS*R*CPS)+(2.0*HG)/(RHOS*DX*CPS))
!
! CALCULATE THE NEW TEMPERATURE AT THIS LOCATION
!
TSNEW(N) = ((2.0*HG*DT)/(RHOS*DX*CPS))*(TAW-TSOLD(N))-((HCL(N) *C0*DT)/(RHOS*R*CPS))*(TSOLD(N)-TLOLD(M))&
-((2.0*KS*DT)/((DX**2.0)*RHOS*CPS))*(TSOLD(N)-TSOLD(N-1))+TSOLD(N)
!
! CALCULATE STABILITY CONSTRAMT
!
STAB3 = 1.0/((2.0*KS)/((DX**2.0)*RHOS*CPS)+(HCL(N)*C0)/(RHOS*R*CPS)+(2.0*HG)/(RHOS*DX*CPS))
END IF
!
! CHECK TIME STEPS AGAINST STABILITY CONSTRAINTS TO PREVENT INSTABILITY
! IF IT FAILS, THE PROGRAM WRITES THE FOLLOWING OUTPUT AND STOPS THE
!PROGRAM
!
IF ((DT.GT.STAB1).OR.(DT.GT.STAB2).OR.(DT.GT.STAB3)) THEN
OPEN(UNIT= 12,FILE=FILE3)
WRITE (12,*) 'STABILITY CONSTRAINT VIOLATION'
WRITE (12,*) 'STAB1 = ', STAB1
WRITE (12,*) 'STAB2 = ', STAB2
WRITE (12,*) 'STAB3 = ', STAB3
WRITE (12,*) 'DT USED = ',DT
CLOSE(12)
CLOSE(14)
STOP
END IF
!
! END THE MAIN LOOP
!
END DO
RETURN
!
! END TSOLID SUBROUTINE
!

END
!
